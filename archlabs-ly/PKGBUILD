# Maintainer: Nathaniel Maia <natemaia10@gmail.com>

pkgname=archlabs-ly
pkgver=0.5.3
pkgrel=2
pkgdesc="TUI display manager"
arch=('x86_64')
url="https://github.com/nullgemm/ly"
license=('custom:WTFPL')
makedepends=('git')
depends=('pam' 'xorg-xauth')
provides=('ly')
conflicts=('ly-git' 'ly' 'python-ly-git')
backup=('etc/ly/config.ini')
source=("git+${url}.git#tag=v${pkgver}"
        "git+https://github.com/nullgemm/argoat.git"
        "git+https://github.com/nullgemm/configator.git"
        "git+https://github.com/nullgemm/dragonfail.git"
        "git+https://github.com/nullgemm/termbox_next.git")
sha256sums=('SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP')

prepare() {
	cd ly
	git submodule init
	for _i in argoat configator dragonfail termbox_next; do
		git config submodule.sub/${_i}.url $srcdir/${_i}
	done
	git submodule update
}

package() {
	cd ly
	make
	make DESTDIR="$pkgdir" install
	install -Dm644 license.md "${pkgdir}/usr/share/licenses/ly/WTFPL"
}
