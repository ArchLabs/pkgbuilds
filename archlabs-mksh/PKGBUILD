# Maintainer: Nathaniel Maia <natemaia10@gmail.com>

pkgname=archlabs-mksh
_pkgname=mksh
_pkgver=R59c
pkgver=59.c
pkgrel=2
pkgdesc='MirBSD Korn Shell'
url='https://www.mirbsd.org/mksh.htm'
license=('custom')
arch=('x86_64')
depends=('glibc')
checkdepends=('ed')
provides=('mksh')
conflicts=('mksh' 'mksh-git' 'mksh-static' 'mksh-static-git')
install=mksh.install
source=("https://www.mirbsd.org/MirOS/dist/mir/mksh/$_pkgname-$_pkgver.tgz")
sha256sums=('77ae1665a337f1c48c61d6b961db3e52119b38e58884d1c89684af31f87bc506')

check() {
  cd "$_pkgname"
  PERL=/usr/bin/perl ./test.sh
}

build() {
  cd "$_pkgname"
  sh Build.sh -r
}

package() {
  cd "$_pkgname"
  install -D -m 755 mksh "$pkgdir/usr/bin/mksh"
  install -D -m 644 mksh.1 "$pkgdir/usr/share/man/man1/mksh.1"
  install -D -m 644 dot.mkshrc "$pkgdir/etc/skel/.mkshrc"

  mkdir -p "$pkgdir/usr/share/licenses/mksh/"
  sed -n '/Copyright/,/\*\//p' main.c > "$pkgdir/usr/share/licenses/mksh/LICENSE"
}
